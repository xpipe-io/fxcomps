plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply from: 'publish.gradle'

version '0.1'
group 'io.xpipe'
archivesBaseName = 'fxcomps'

repositories {
    mavenCentral()
}

java {
    modularity.inferModulePath = true
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

def currentOS = DefaultNativePlatform.currentOperatingSystem;
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

dependencies {
    implementation "org.openjfx:javafx-base:18-ea+4:${platform}"
    implementation "org.openjfx:javafx-controls:18-ea+4:${platform}"
    implementation "org.openjfx:javafx-graphics:18-ea+4:${platform}"
    implementation "org.openjfx:javafx-media:18-ea+4:${platform}"
    implementation "org.openjfx:javafx-web:18-ea+4:${platform}"

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

javadoc{
    source = sourceSets.main.allJava
    options {
        addStringOption('-release', '17')
        addStringOption('link', 'https://docs.oracle.com/en/java/javase/17/docs/api/')
        addBooleanOption('html5', true)
        addStringOption('Xdoclint:none', '-quiet')
    }
}