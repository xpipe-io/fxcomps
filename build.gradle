plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

if(project == rootProject) {
    apply plugin: "io.codearte.nexus-staging"
}


version '0.2.1'
group 'io.xpipe'
archivesBaseName = 'fxcomps'

apply from: 'publish.gradle'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    modularity.inferModulePath = true
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

def currentOS = DefaultNativePlatform.currentOperatingSystem;
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

dependencies {
    compileOnly "org.openjfx:javafx-base:18:${platform}"
    compileOnly "org.openjfx:javafx-controls:18:${platform}"
    compileOnly "org.openjfx:javafx-graphics:18:${platform}"
    compileOnly "org.openjfx:javafx-media:18:${platform}"
    compileOnly "org.openjfx:javafx-web:18:${platform}"

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

javadoc{
    source = sourceSets.main.allJava
    options {
        addStringOption('-release', '17')
        addStringOption('link', 'https://docs.oracle.com/en/java/javase/17/docs/api/')
        addBooleanOption('html5', true)
        addStringOption('Xdoclint:none', '-quiet')
    }
}