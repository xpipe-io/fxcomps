java {
    withJavadocJar()
    withSourcesJar()
}

def isSnapshot = project.getVersion().endsWith('SNAPSHOT')
def repoUrl = isSnapshot ? 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        : 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
def user = project.hasProperty('sonatypeUsername') ? project.property('sonatypeUsername') : System.getenv('SONATYPE_USERNAME')
def pass = project.hasProperty('sonatypePassword') ? project.property('sonatypePassword') : System.getenv('SONATYPE_PASSWORD')

if (!isSnapshot) {
    publish.finalizedBy(rootProject.getTasks().getByName('closeAndReleaseRepository'))
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'FxComps'
                description = 'The FxComps library provides a new approach to creating JavaFX interfaces and offers a quicker and more robust user interface development workflow.'
                url = 'https://github.com/xpipe-io/fxcomps'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/xpipe-io/fxcomps/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'crschnick'
                        name = 'Christopher Schnick'
                        email = 'crschnick@xpipe.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/xpipe-io/fxcomps.git'
                    developerConnection = 'scm:git:ssh://github.com/xpipe-io/fxcomps.git'
                    url = 'https://github.com/xpipe-io/fxcomps'
                }
            }
        }
    }

    repositories {
        maven {
            setUrl repoUrl
            credentials {
                setUsername user
                setPassword pass
            }
        }
    }
}

def signingKeyId = project.hasProperty('signingKeyId') ? project.property("signingKeyId") : System.getenv('GPG_KEY_ID')
def signingKey = project.hasProperty('signingKeyFile') ? file(project.property("signingKeyFile")).text : System.getenv('GPG_KEY')
def signingPassword = project.hasProperty('signingPassword') ? project.property("signingPassword") : System.getenv('GPG_KEY_PASSWORD')

signing {
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword != null ? signingPassword : '')

    sign publishing.publications.mavenJava
}

nexusStaging {
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    packageGroup = "io.xpipe"
    username = user
    password = pass
}